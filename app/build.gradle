apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// junit5 doesn't support android projects out of the box
apply plugin: "de.mannodermaus.android-junit5"

android {

    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "siarhei.luskanau.iot.doorbell"
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "siarhei.luskanau.iot.doorbell.MockTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
        junitPlatform {
            filters {
                engines {
                    include 'spek'
                }
            }
            //noinspection UnnecessaryQualifiedReference
            details org.junit.platform.console.options.Details.FLAT
        }
    }
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies

    implementation project(':app_base')
    implementation project(':base_firebase')
    implementation project(':base_camera')

    kapt appDependencies.databindingCompiler

    kapt appDependencies.daggerCompiler
    kapt appDependencies.daggerAndroidProcessor
    implementation appDependencies.dagger
    implementation appDependencies.daggerAndroidSupport

    implementation appDependencies.kotlinStdlib
    implementation appDependencies.timber
    implementation appDependencies.supportV4

    implementation appDependencies.rxJava
    implementation appDependencies.rxAndroid

    kapt appDependencies.archLifecycleCompiler
    implementation appDependencies.archLifecycleExtensions
    implementation appDependencies.archLifecycleReactivestreams
    implementation appDependencies.supportDesign
    implementation appDependencies.supportCardview
    implementation appDependencies.constraintLayout

    implementation appDependencies.gson

    compileOnly appDependencies.androidThings

    //Development
    debugImplementation appDependencies.leakCanary
    releaseImplementation appDependencies.leakCanaryNoOp
    testImplementation appDependencies.leakCanaryNoOp

    //test instrumentation
    testImplementation appDependencies.junit4
    androidTestImplementation appDependencies.androidTestRunner
    androidTestImplementation appDependencies.espresso
    androidTestImplementation(appDependencies.mockitoKotlin) {
        exclude group: 'org.jetbrains.kotlin'
    }

    //test
    testImplementation(appDependencies.mockitoKotlin) {
        exclude group: 'org.jetbrains.kotlin'
    }

    testImplementation junit5.unitTests()

    testImplementation("org.jetbrains.spek:spek-api:$project.spekVersion") {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation("org.jetbrains.spek:spek-junit-platform-engine:$project.spekVersion") {
        exclude group: "org.junit.platform"
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$project.kotlinVersion"
}