apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"

android {

    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "siarhei.luskanau.iot.doorbell"
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "siarhei.luskanau.iot.doorbell.MockTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies

    implementation project(':app_base')
    implementation project(':base_camera')
    implementation project(':base_file')
    implementation project(':base_firebase')
    implementation project(':base_persistence')
    implementation project(':base_cache')
    implementation project(':base_work_manager')

    kapt appDependencies.daggerCompiler
    kapt appDependencies.daggerAndroidProcessor
    implementation appDependencies.dagger
    implementation appDependencies.daggerAndroid

    implementation appDependencies.kotlinStdlib

    implementation appDependencies.rxJava
    implementation appDependencies.rxAndroid

    kapt appDependencies.archLifecycleCompiler
    implementation appDependencies.archLifecycleExtensions
    implementation appDependencies.archLifecycleReactivestreams
    implementation appDependencies.archPaging
    implementation appDependencies.supportDesign
    implementation appDependencies.supportCardview
    implementation appDependencies.constraintLayout

    implementation appDependencies.gson
    implementation appDependencies.timber

    compileOnly appDependencies.androidThings

    implementation appDependencies.archWorkManager

    //Development
    debugImplementation appDependencies.leakCanary
    releaseImplementation appDependencies.leakCanaryNoOp
    testImplementation appDependencies.leakCanaryNoOp

    //test
    testImplementation appDependencies.junit4
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.2.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$project.kotlinVersion"

    testImplementation(appDependencies.mockitoKotlin) {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation("org.spekframework.spek2:spek-dsl-jvm:$project.spekVersion") {
        exclude group: "org.jetbrains.kotlin"
    }

    //test instrumentation
    androidTestImplementation appDependencies.espresso
    androidTestImplementation appDependencies.androidTestRunner
    androidTestImplementation("org.jetbrains.kotlin:kotlin-test-junit:$project.kotlinVersion")
    androidTestImplementation("org.jetbrains.kotlin:kotlin-reflect:$project.kotlinVersion")
    androidTestImplementation(appDependencies.mockitoKotlin) {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation("org.spekframework.spek2:spek-runner-junit5:$project.spekVersion") {
        exclude group: "org.junit.platform"
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$project.kotlinVersion"
}